{-# LANGUAGE ExistentialQuantification #-}
{-# LANGUAGE DeriveGeneric #-}
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE OverloadedStrings #-}

-- | Datatypes for analysis application API.

module DataAnalysis.Application.Types
  (VisualizationType(..)
  ,ExportType(..)
  ,AnalysisAppConfig(..)
  ,DataPoint(..)
  ,pointText
  ,pointDouble
  ,App(..)
  ,Source(..)
  ,GenericApp(..))
  where

import Control.Concurrent.STM
import Control.Lens (Lens',lens)
import Data.ByteString.Lazy (ByteString)
import Data.Default
import Data.IntMap (IntMap)
import Data.Text (Text,pack)
import Data.Time
import GHC.Generics
import Yesod

-- | The type of visualization used to show some data.
data VisualizationType
  = LineChart
  | BarChart
  | PieChart
  deriving (Show,Enum,Eq)

-- | Default chart type to use for data.
instance Default VisualizationType where
  def = LineChart

-- | The format to export data generated by an analysis.
data ExportType
  = JsonData
  | CsvData
  | XmlData
  deriving (Show,Enum,Eq)

-- | Default export type used if none is specified.
instance Default ExportType where
  def = JsonData

-- | A data point which can be rendered onto a chart of some kind.
data DataPoint
  = TripleText Text Text Double -- ^ Can be rendered onto a line or bar chart.
  | TripleDouble Text Double Double -- ^
  | Tuple Text Double   -- ^ Can be rendered onto a pie, line or bar chart, etc.
  deriving (Show,Generic)

-- | Lens for the text of a data point.
pointText :: Lens' DataPoint Text
pointText = lens get' set'
  where get' d =
          case d of
            TripleText t _ _ -> t
            TripleDouble t _ _ -> t
            Tuple t _ -> t
        set' d v =
          case d of
            TripleText _ a b -> TripleText v a b
            TripleDouble _ a b -> TripleDouble v a b
            Tuple _ a -> Tuple v a

-- | Lens for the double value of a data point.
pointDouble :: Lens' DataPoint Double
pointDouble = lens get' set'
  where get' d =
          case d of
            TripleText _ _ v -> v
            TripleDouble _ _ v -> v
            Tuple _ v -> v
        set' d v =
          case d of
            TripleText a b _ -> TripleText a b v
            TripleDouble a b _ -> TripleDouble a b v
            Tuple a _ -> Tuple a v

-- | Data points can be sent to the client in JSON form, but the value
--   in the triple should at least be enumerable.
instance ToJSON DataPoint

-- | Configuration for the analysis app.
data AnalysisAppConfig params source = AnalysisAppConfig
  { analysisFunc :: params -> [source] -> IO [DataPoint]
  , analysisTitle :: Text
  , analysisDefVisualization :: VisualizationType
  , analysisDefDataExport :: ExportType
  , analysisParser :: ByteString -> IO (Maybe [source])
  , analysisPrint :: source -> Text
  , analysisForm :: Html -> MForm (HandlerT GenericApp IO)
                                  (FormResult params,WidgetT GenericApp IO ())
  }

-- | Default configuration, doesn't produce any results.
instance (Show source,Default params) => Default (AnalysisAppConfig params source) where
  def = AnalysisAppConfig
    { analysisFunc = \_params _data -> return []
    , analysisTitle = "Untitled Analysis Application"
    , analysisDefVisualization = def
    , analysisDefDataExport = def
    , analysisParser = const (return Nothing)
    , analysisPrint = \source -> pack (show source)
    , analysisForm = const undefined
    }

-- | Yesod app type.
data App source params = App
  { appParser     :: !(ByteString -> IO (Maybe [source]))
  , appPrinter    :: !(source -> Text)
  , appAnalyzer   :: !(params -> [source] -> IO [DataPoint])
  , appCounter    :: !(TVar Int)
  , appStore      :: !(TVar (IntMap (Source source)))
  , appTitle      :: !Text
  , appParamsForm :: !(Html -> MForm (HandlerT GenericApp IO)
                                     (FormResult params,WidgetT GenericApp IO ()))
  }

-- | A generic app.
data GenericApp =
  forall source params.
  Default params =>
  GApp (App source params)

-- | An imported data source.
data Source source = Source
  { srcParsed    :: ![source]
  , srcTimestamp :: !UTCTime
  }
